import team.ebi.gradle.TaskOreDeploy

plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'maven-publish'
    id 'groovy'
}

repositories {
    mavenCentral()
}

def major = 1
def minor = 0
def patch = 1

def id = "multicurrencies"

static def getBuildNumber() {
    if (System.getenv("BUILD_NUMBER") != null) {
        return System.getenv("BUILD_NUMBER").toString()
    }
    return ""
}

static def getStable() {
    if (System.getenv("GITHUB_REF") == null || System.getenv("GITHUB_REF").endsWith("-dev")) {
        return "unstable"
    }
    return ""
}

group = 'me.settingdust'
version = "$major.$minor.$patch${if (!getBuildNumber().isBlank()) "-${getBuildNumber()}" else ""}${if (!getStable().isBlank()) "-${getStable()}" else ""}"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation 'org.spongepowered:spongeapi:7.2.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.2.0'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SettingDust/multi-currencies")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        create("gpr", MavenPublication.class) {
            from(components.java)
        }
    }
}

task oreDeploy(type: TaskOreDeploy) {
    pluginId = id
    apiKey = project.findProperty("ore.key") ?: System.getenv("ORE_KEY")
    setPluginInfo(
            recommended: true,
            createForumPost: true,
//            description: "v0.4.0",
            tags: [Channel: "PreRelease"]
    )
    jarTask = "jar"
}

sponge.plugin.id = 'multi-currencies'
